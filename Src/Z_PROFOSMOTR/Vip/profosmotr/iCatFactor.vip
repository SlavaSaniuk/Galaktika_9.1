#doc
Справочник факторов(Белтелеком)
Значения справочника хранятся в таблице DISTDOCHIERARCHY,
((
and constTypeCatFactor == DISTDOCHIERARCHY.WDOCTYPE
))
DISTDOCHIERARCHY.Sname ---- наименование фактора
#end

Interface iCatFactor 'Справочник факторов' EscClose, Cyan, DoAccept;
  Show at (7,3,73,19);

var
wIntMode   : word;
coFactor : comp;

Create view
as select
*
from
 DISTDOCHIERARCHY
,DISTDOCHIERARCHY DISTDOCHIERARCHYnabor     //таблица для значений набора
,DISTDOCHIERARCHY DISTDOCHIERARCHYcheck
Where
((
    constTypeCatFactor    == DISTDOCHIERARCHY.WDOCTYPE

and constTypeNabor        == DISTDOCHIERARCHYnabor.WDOCTYPE
and DISTDOCHIERARCHY.nrec == DISTDOCHIERARCHYnabor.CUSERID(noindex)
))
;

Parameters
 wIntMode   //0(cgiNo) - обычный режим, 1(cgiPick) - режим выбора
,coFactor;  //нрек фактора

Procedure DISTDOCHIERARCHY_SetDefault;
{
  ClearBuffer(#DISTDOCHIERARCHY);
  RescanPanel(#DISTDOCHIERARCHY);

  DISTDOCHIERARCHY.nRec       := GetNextNRec(#DISTDOCHIERARCHY, 0);;
  DISTDOCHIERARCHY.WDOCTYPE   := constTypeCatFactor;
}

Function DISTDOCHIERARCHY_CheckRecord:boolean;
{
   DISTDOCHIERARCHY_CheckRecord:=false;

   DISTDOCHIERARCHY.Sname:= trim(DISTDOCHIERARCHY.Sname);

   if (DISTDOCHIERARCHY.Sname = '')
     {
      message('Задайте наименование фактора или удалите запись',CancelButton);
      SelectField(#DISTDOCHIERARCHY.Sname);
      exit;
     }
   if (RecordExists DISTDOCHIERARCHYcheck where ((constTypeCatFactor     == DISTDOCHIERARCHYcheck.WDOCTYPE
                                            and   DISTDOCHIERARCHY.Sname == DISTDOCHIERARCHYcheck.sname(noindex)
                                            and  (DISTDOCHIERARCHY.nrec  <>DISTDOCHIERARCHYcheck.nrec)))=tsok)
      {
       message('Фактор с таким наименованием уже существует. Переименуйте либо удалите запись!',CancelButton);
       SelectField(#DISTDOCHIERARCHY.Sname);
       exit;
      }

   DISTDOCHIERARCHY_CheckRecord:=true;
}

Function DISTDOCHIERARCHY_DeleteRecord:boolean;
{
  DISTDOCHIERARCHY_DeleteRecord:=false;
  if(RecordExists DISTDOCHIERARCHYnabor = tsok)
    {
       message('Удалять существующую запись из каталога нельзя.'#13+
       'В справочнике "Набор соответствий" существуют записи ссылающиеся на нее!"',Warning);
       exit;
    }
  else
    {
        if (message('Удалить текущую запись?',YesNo)=yes)
           {
             delete current DISTDOCHIERARCHY;
           }
        else
           {
             exit;
           }
    }
  DISTDOCHIERARCHY_DeleteRecord:=true;
}

Window wCatFactorEdit 'Редактирование справочника факторов' EscClose;
  show at (7,3,73,20);
Panel panCatFactorEdit;
 table DISTDOCHIERARCHY;
  Browse brCatFactorEdit;
   Fields
     DISTDOCHIERARCHY.Sname     #3'Наименование' ('Наименование фактора',, sci178Esc):[28];
  end;

HandleEvent
cmSetDefault:
{
  DISTDOCHIERARCHY_SetDefault;
  SelectField(#DISTDOCHIERARCHY.Sname);
  PutCommand(cmEdit);
}
cmCheckRecord:
{
 if (not DISTDOCHIERARCHY_CheckRecord)
    {
      Abort;
    }
}
cmInsertRecord:
{
  insert current DISTDOCHIERARCHY;
}
cmUpdaterecord:
{
  update current DISTDOCHIERARCHY;
}
cmDeleteRecord:
{
  if (not DISTDOCHIERARCHY_DeleteRecord)
    {
      Abort;
    }
}
end;
end;

HandleEvent
cmDone:
{
  if(not UpDateTable)
    {
      abort;
    }
}
end;
end;

Panel panCatFactor;
  table DISTDOCHIERARCHY;
    browse brCatFactor (,,sci147EnEsc);
      fields
        DISTDOCHIERARCHY.SNAME      #3'Наименование' ('Наименование фактора'):[28],protect;
    end;
end;

Handleevent
cmInit:
{
   if (wIntMode=1)
     {
       SetTitle('Выбор фактора');
     }

   if ( not isValid(#DISTDOCHIERARCHY))
     {
      if (message('Справочник факторов пуст.' +
                 ''#13'Хотите заполнить его?',YesNo) <> cmYes)
         {
           abort;
           exit;
         }
      else
         {
           PutCommand(cmInsert);
         }
     }

}
cmAddNewRec:
{
 PutCommand(cmInsert);
 PutCommand(cmEdit);
}
cmDefault:
{
 if (wIntMode=1)
  {
    coFactor := DISTDOCHIERARCHY.nrec;
  }
 else
  {
    PutCommand(cmEdit);
    Abort;
  }
}
cmEdit:
{
 if (CurWindow <> wCatFactorEdit)
   {
     RunWindow(wCatFactorEdit);
   }
}
cmHotKeys:
{
  case CurTable of
      #DISTDOCHIERARCHY:
        {
         PutHotCommand(RunMenu('MenKatFactorMain'));
        }
    end;
}
cmImp:
{
  if(RunInterface('iImportKatFact')=cmCancel)
    {
      exit;
    }
  else
    {
      ReReadRecord(#DISTDOCHIERARCHY);
      message('Операция завершена!');
    }
}
end;
End.

MenKatFactorMain Menu
{
  - 'Импорт справочника из файла формата xls...', cmImp   ,'Импорт справочника из файла формата xls...', hcSwiftMAll, 'Alt+I', kbAltI, sci1Esc,,,,bmpImpDoc  ;
}
